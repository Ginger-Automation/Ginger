; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Ginger by amdocs"
#define MyAppVersion "5.1.1.242"
#define MyAppPublisher "Amdocs"
#define MyAppURL "http://https://github.com/Ginger-Automation"
#define MyAppExeName "Ginger.exe"


[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{456AA952-D6CD-4D37-B213-F146E69A15E1}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\Amdocs\{#MyAppName}
LicenseFile=D:\a\Ginger\Ginger\Extensions\License.rtf
OutputDir=D:\a\Installers
OutputBaseFilename="Ginger"
SetupIconFile=D:\a\Ginger\Ginger\Extensions\GingerIconNew.ico
UninstallDisplayIcon={app}\{#MyAppExeName}
UninstallDisplayName= {#MyAppName} 
Compression=lzma
SolidCompression=yes
UsePreviousTasks=no
DisableReadyPage=no
DisableReadyMemo=no
AlwaysShowGroupOnReadyPage=yes
WizardStyle=modern

[InstallDelete]
Type: filesandordirs; Name: {app}\*

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]


[Files]
Source: "D:\a\Ginger\Ginger\Ginger\Ginger\bin\Release\net8.0-windows10.0.17763.0\Ginger.exe"; DestDir: "{app}"; Flags: ignoreversion; BeforeInstall:DetectAndInstallPrerequisites;
Source: "D:\a\Ginger\Ginger\Ginger\Ginger\bin\Release\net8.0-windows10.0.17763.0\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs;
Source: "D:\a\Ginger\Ginger\Extensions\DotnetDependencies\netcorecheck_x64.exe"; Flags: dontcopy deleteafterinstall noencryption
Source: "D:\a\Ginger\Ginger\Extensions\DotnetDependencies\windowsdesktop-runtime-8.0.22-win-x64.exe"; DestDir: {tmp}; Flags: dontcopy deleteafterinstall noencryption;
Source: "D:\a\Ginger\Ginger\Extensions\DotnetDependencies\aspnetcore-runtime-8.0.22-win-x64.exe"; DestDir: {tmp}; Flags: dontcopy deleteafterinstall noencryption;
Source: "D:\a\Ginger\Ginger\Extensions\DotnetDependencies\AccessDatabaseEngine_X64.exe"; DestDir: "{tmp}"; Flags: dontcopy deleteafterinstall noencryption
Source: "D:\a\Ginger\Ginger\Ginger\SetGingerExe.bat"; DestDir: "{app}"; Flags: ignoreversion
;NOTE: Don't use "Flags: ignoreversion" on any shared system files


[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}" 

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall shellexec skipifsilent;
Filename: "{app}\SetGingerExe.bat"; Flags: runhidden;
[Code]

//################################################### General Parameters #######################################
var 
  _wizpSettingsTypeSelectionPage: TInputOptionWizardPage;
  _IsUserProfileExist: boolean; 
  _IsCustomizeSettingSelected: boolean; 
  _wizpUserTypeSelectionPage: TInputOptionWizardPage; 
  ChosenUserType: String;
  ChosenUserTerminology: String;
  _strUserType: String;
  _strUserTypeLable: String;
  _wizpTerminologyTypePage: TInputOptionWizardPage;
  _strTerminologyType: String;
  XMLNode: Variant;
  XMLDocument: Variant; 
  XmlElement:  Variant; 
  strUserProfileXMLFilePath: String;

//################################################### Functions & Procedures #######################################
function Dependency_IsNetCoreInstalled(const Version: String): Boolean;
var
  ResultCode: Integer;
begin
  // source code: https://github.com/dotnet/deployment-tools/tree/master/src/clickonce/native/projects/NetCoreCheck
  if not FileExists(ExpandConstant('{tmp}{\}') + 'netcorecheck_x64.exe') then begin
    ExtractTemporaryFile('netcorecheck_x64.exe');
  end;
  Result := ShellExec('', ExpandConstant('{tmp}{\}') + 'netcorecheck_x64.exe', Version, '', SW_HIDE, ewWaitUntilTerminated, ResultCode) and (ResultCode = 0);
end;

function IsAccessEngineInstalled: Boolean;
var
  productCode: string;
begin
  productCode := '{90140000-2005-0000-0000-0000000FF1CE}';
  Result := (RegValueExists(HKLM, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\' + productCode + '_X64', 'DisplayName') or
             RegValueExists(HKLM, 'SOFTWARE\Wow6432Node\Microsoft\Windows\CurrentVersion\Uninstall\' + productCode + '_X64', 'DisplayName'));
end;

procedure DetectAndInstallPrerequisites;
var
  StatusText: string;
  ResultCode: Integer;
begin
  StatusText := WizardForm.StatusLabel.Caption;   
  WizardForm.StatusLabel.Caption := 'Detecting required .Net runtimes...';
  WizardForm.ProgressGauge.Style := npbstMarquee;
  try
    If not Dependency_IsNetCoreInstalled('Microsoft.WindowsDesktop.App 8.0.22') then
    begin
      WizardForm.StatusLabel.Caption := 'Installing Microsoft.WindowsDesktop.App Runtime 8.0.22...';
      if not FileExists(ExpandConstant('{tmp}\windowsdesktop-runtime-8.0.22-win-x64.exe')) then begin
        ExtractTemporaryFile('windowsdesktop-runtime-8.0.22-win-x64.exe');
      end;
      if not Exec(ExpandConstant('{tmp}\windowsdesktop-runtime-8.0.22-win-x64.exe'), '/q /norestart', '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
        begin
          { you can interact with the user that the installation failed }
          MsgBox('.NET Desktop runtime 8.0.22 installation failed with code: ' + IntToStr(ResultCode) + '. Please install it manually.',
            mbError, MB_OK);
      end;
    end;
    If not Dependency_IsNetCoreInstalled('Microsoft.AspNetCore.App 8.0.22') then 
    begin
      WizardForm.StatusLabel.Caption := 'Installing Microsoft.AspNetCore.App Runtime 8.0.22...';
      if not FileExists(ExpandConstant('{tmp}\aspnetcore-runtime-8.0.22-win-x64.exe')) then begin
        ExtractTemporaryFile('aspnetcore-runtime-8.0.22-win-x64.exe');
      end;
      if not Exec(ExpandConstant('{tmp}\aspnetcore-runtime-8.0.22-win-x64.exe'), '/q /norestart', '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
        begin
          { you can interact with the user that the installation failed }
          MsgBox('.NET ASP Net Core 8.0.22 installation failed with code: ' + IntToStr(ResultCode) + '. Please install it manually.',
            mbError, MB_OK);
      end;
    end;
    if not IsAccessEngineInstalled then 
    begin
      WizardForm.StatusLabel.Caption := 'Installing Microsoft Access database engine 2010 (64-bit)...';  
      if not FileExists(ExpandConstant('{tmp}\AccessDatabaseEngine_X64.exe')) then begin 
        ExtractTemporaryFile('AccessDatabaseEngine_X64.exe');
      end;
      if not Exec(ExpandConstant('{tmp}\AccessDatabaseEngine_X64.exe'), '/q /norestart', '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then 
        begin
        MsgBox('Error installing Microsoft Access database engine 2010 (64-bit).', mbError, MB_OK);        
      end;
    end;

  finally
    WizardForm.StatusLabel.Caption := StatusText;
    WizardForm.ProgressGauge.Style := npbstNormal;
  end;
end;

function CheckIfGingerInstalled: boolean;
begin
if ( not FileExists(ExpandConstant('C:\Program Files (x86)\Amdocs\Amdocs BEAT Ginger Automation\unins000.exe')))  then
begin
  if (  FileExists(ExpandConstant('C:\Program Files (x86)\Amdocs\Amdocs BEAT Ginger Automation\ginger.exe')))      then
  begin
  result :=true;
  end;
end;
if ( not FileExists(ExpandConstant('C:\Program Files\Amdocs\Amdocs BEAT Ginger Automation\unins000.exe')))  then
begin
  if (  FileExists(ExpandConstant('C:\Program Files\Amdocs\Amdocs BEAT Ginger Automation\ginger.exe')))      then
  begin
  result :=true;
  end;
end;
if ( not FileExists(ExpandConstant('C:\Program Files (x86)\Amdocs\Ginger by amdocs\unins000.exe')))  then
begin
  if (  FileExists(ExpandConstant('C:\Program Files (x86)\Amdocs\Ginger by amdocs\ginger.exe')))      then
  begin
  result :=true;
  end;
end;                                                                
if ( not FileExists(ExpandConstant('C:\Program Files\Amdocs\Ginger by amdocs\unins000.exe')))  then
begin
  if (  FileExists(ExpandConstant('C:\Program Files\Amdocs\Ginger by amdocs\ginger.exe')))      then
  begin
  result :=true;
  end;
end;
end;

function InitializeSetup: boolean;
begin
  if  CheckIfGingerInstalled then
    Begin
      result :=false;
      MsgBox('Please Uninstall Ginger by amdocs Before Proceeding '#13#10''#13#10'Note: If you already uninstall Ginger and still getting this message please menually delete the Ginger installation folder under the path:' + ExpandConstant('{app}') , mbError, MB_OK);
    end
  else 
    begin
      result:=true
    end;
end;


Procedure InitializeWizard();
Var
  left, leftInc, top, topInc: Integer;

  StaticText: TNewStaticText;
Begin
  _IsUserProfileExist := False;
  strUserProfileXMLFilePath:=  ExpandConstant('{userappdata}') + '\amdocs\Ginger\Ginger.UserProfile.xml';
  if (FileExists(strUserProfileXMLFilePath)) then
  Begin
    //Load XML
      _IsUserProfileExist := true;
      XMLDocument := CreateOleObject('Msxml2.DOMDocument.6.0'); 
  try 
    
    XMLDocument.async := False;
    XMLDocument.load(strUserProfileXMLFilePath);
    XMLNode := XMLDocument.selectSingleNode('//GingerRepositoryItem//UserProfile');
	if (IDispatch(XMLNode) = nil) then
    begin
    XMLNode := XMLDocument.selectSingleNode('//Ginger.UserProfile');
    end;
    if (XMLNode.getAttribute('UserType') = null) then 
    begin
        ChosenUserType := 'Regular';
    end else
    begin
        ChosenUserType := XMLNode.getAttribute('UserType');
    end;
    ChosenUserTerminology := XMLNode.getAttribute('TerminologyDictionaryType');

  Except
    MsgBox('An error occured while updating the User Profile XML file' + #13#10 + GetExceptionMessage, mbError, MB_OK);
  end;


  end
  Else
  Begin
    ChosenUserType := 'Regular';
    ChosenUserTerminology := 'Default';
    _IsCustomizeSettingSelected := True;
  end;

  if (ChosenUserType = 'Regular') Then
  Begin
    _strUserTypeLable:='Automation (Technical) user';
  end
  Else if (ChosenUserType = 'Business') Then
  Begin
    _strUserTypeLable:='Business user';
  end;
  
  _wizpSettingsTypeSelectionPage :=  CreateInputOptionPage(wpSelectDir, 'Ginger Profile Settings', '','Your current profile settings are:  User type: '+_strUserTypeLable +', Terminology: '+ ChosenUserTerminology  +' '#13#10'Choose your configuration preference and then click Next.' , True, False);
  if (_IsUserProfileExist) then
  Begin
    _wizpSettingsTypeSelectionPage.Add('Keep Current Settings');
    _IsUserProfileExist := true;
  end;
  _wizpSettingsTypeSelectionPage.Add('Customize Settings');
  _wizpSettingsTypeSelectionPage.Values[0] := True;   

   
  //User type selection Page Init 
  _strUserType:='Regular';
  _strUserTypeLable:='Automation (Technical) user';
  _wizpUserTypeSelectionPage:= CreateInputOptionPage(_wizpSettingsTypeSelectionPage.ID, 'Ginger User Type', '','Please select the default user type. '#13#10''#13#10'Available User Types: '#13#10'1. Automation (technical) user: Design and execution functionalities '#13#10'2. Business user: Design functionalities '#13#10' '#13#10'Note: the user type can be changed at any time from Ginger UI. '#13#10' '#13#10'Please Select: ' , True, False);
  _wizpUserTypeSelectionPage.Add('Automation (Technical) user');
  _wizpUserTypeSelectionPage.Add('Business user');
  if(ChosenUserType = 'Business') then
  begin
    _wizpUserTypeSelectionPage.Values[1] := True;
  end
  Else  
  begin
    _wizpUserTypeSelectionPage.Values[0] := True;
  end;

    //Terminology type selection Page Init 
  _strTerminologyType:='Default';
  _wizpTerminologyTypePage:= CreateInputOptionPage(_wizpUserTypeSelectionPage.ID, 'Ginger Default Terminology', '','Please select the preferred default terminology type to be used. '#13#10''#13#10'Available Terminologies: '#13#10'1. Default- Ginger default items names: Run Set/Business Flow/Activities Group/Activity/Variable '#13#10'2. Testing- Testing (ALM applications) related terms like: Calend;ar/Test Set/Test Case/Step/Parameter '#13#10'3. Gherkin- BDD related terms like: Business Flow Feature/Scenario/Step '#13#10' '#13#10'Note: the terminology type can be changed at any time from Ginger UI. '#13#10'Please Select: ', True, False);
  _wizpTerminologyTypePage.Add('Default');
  _wizpTerminologyTypePage.Add('Testing');
  _wizpTerminologyTypePage.Add('Gherkin');
 

  if(ChosenUserTerminology = 'Gherkin') then
  begin
    _wizpTerminologyTypePage.Values[2] := True;
  end
  Else if(ChosenUserTerminology = 'Testing')  then
  begin
    _wizpTerminologyTypePage.Values[1] := True;
  end
  Else 
  begin
    _wizpTerminologyTypePage.Values[0] := True;
  end;  
end;

Function ShouldSkipPage(PageID: Integer): Boolean;
Begin
  Log('ShouldSkipPage(' + IntToStr(PageID) + ') called');
  { Skip wpInfoBefore page; show all others }
  Case PageID Of
    wpInfoBefore:
      Result := True;
    
    wpSelectDir:
      Result := False;

    _wizpSettingsTypeSelectionPage.ID:
      Result := not _IsUserProfileExist;

    _wizpUserTypeSelectionPage.ID:
      Result := not _IsCustomizeSettingSelected;
         
    _wizpTerminologyTypePage.ID:
      Result := not _IsCustomizeSettingSelected;

    wpReady:
      Result := False;
  end;
end;


Function NextButtonClick(CurPageID: Integer): Boolean;
Var
  resultCode: Integer;
  exeParam: String;  
  strWizardDirValue: String;
Begin
Case CurPageID Of

  _wizpSettingsTypeSelectionPage.ID:
  Begin
  if (_IsUserProfileExist) Then
  Begin
     if (_wizpSettingsTypeSelectionPage.Values[0] = True) Then
     Begin
        _strUserType := ChosenUserType;
        _strTerminologyType := ChosenUserTerminology;
        _IsCustomizeSettingSelected := False;
     end Else If (_wizpSettingsTypeSelectionPage.Values[1] = True) Then
     Begin
        _IsCustomizeSettingSelected := True;
     end;  
  end; //Else 
  if (_strUserType = 'Regular') Then
  Begin
    _strUserTypeLable:='Automation (Technical) user';
  end
  Else if (_strUserType = 'Business') Then
  Begin
    _strUserTypeLable:='Business user';
  end;

 end;


  _wizpUserTypeSelectionPage.ID:
    Begin
      If _wizpUserTypeSelectionPage.Values[0] = True Then
      Begin
        _strUserType:= 'Regular';
        _strUserTypeLable:='Automation (Technical) user';
      end Else 
      Begin
        _strUserType:= 'Business';
        _strUserTypeLable:='Business user';
      end;
      Result := True;
    end;

      _wizpTerminologyTypePage.ID:
    Begin
      If _wizpTerminologyTypePage.Values[0] = True Then
      Begin
        _strTerminologyType:= 'Default';
      end Else If  _wizpTerminologyTypePage.Values[1] = True Then
      Begin
        _strTerminologyType:= 'Testing';
      end Else If  _wizpTerminologyTypePage.Values[2] = True Then
      Begin
        _strTerminologyType:= 'Gherkin';
      end;
      Result := True;
    end;
 end;
 Result := True;
end;


Procedure CurStepChanged(CurStep: TSetupStep);
var 
strWizardDirValue: String;
 Begin
     If CurStep = ssPostInstall Then
     Begin

       strUserProfileXMLFilePath:=  ExpandConstant('{userappdata}') + '\amdocs\Ginger\Ginger.UserProfile.xml';  
         Log('Eliran  ' + strUserProfileXMLFilePath);
       XMLDocument := CreateOleObject('Msxml2.DOMDocument.6.0');
     Try
     Begin
      strWizardDirValue:= AddBackslash(WizardDirValue);
      SaveStringToFile( strWizardDirValue + '\Ginger.InstallationConfiguration.Json', #13#10 + '{ "UserType" : "'+_strUserType+'", "TerminologyDictionaryType": "'+_strTerminologyType+'"}' + #13#10, False);
     end;
     Except
      MsgBox('An error occured while updating the User Profile XML file' + #13#10 + GetExceptionMessage, mbError, MB_OK);
     end;
 end;
end;

Procedure CurPageChanged(CurPageID: Integer);
Begin
    //Next button always available
    Wizardform.NextButton.Enabled := True;

    //Add summary page info
    If CurPageID=wpReady Then
      Begin

       //Summary info
       Wizardform.ReadyMemo.Lines.Add('');
       Wizardform.ReadyMemo.Lines.Add('User Type:'#13#10'      "' + _strUserTypeLable + '"');
       Wizardform.ReadyMemo.Lines.Add('');
       Wizardform.ReadyMemo.Lines.Add('Terminology Type:'#13#10'      "' + _strTerminologyType + '"');
    end;
end;
